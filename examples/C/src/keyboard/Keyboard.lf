/**
 * Output the key code for any key pressed on the keyboard. The timestamp of the output is the
 * physical time at which the key is pressed.
 *
 * This reactor uses curses, a reasonably portable C library for terminal interactions. If your
 * program sends messages to the standard output, then you should print the messages using
 * `lf_print` rather than `printf` so that they are cleanly displayed on the terminal.
 *
 * If a non-zero exit_key parameter is given, then it is the key code for a key that, when pressed,
 * will cause this reactor to call lf_request_stop(), bringing the program to exit.
 *
 * @see ../rhythm/SensorSimulator.lf
 * @author Edward A. Lee
 */
target C {
  keepalive: true,
  cmake-include: ["curses.cmake"]
}

preamble {=
  typedef struct keyboard_input_t {
    void* keypress; // Pointer to physical action.
    int exit_key;   // Keycode for exiting.
  } keyboard_input_t;
=}

reactor KeyboardInput(exit_key: int = 0) {
  output key: int
  physical action keypress: int

  preamble {=
    #include "platform.h"
    #include <curses.h>
    #include <signal.h>
    // Thread to read input characters until an EOF is received.
    // Each time a character is received, schedule a keypress action.
    void* read_input(void* keyboard_input) {
      int c;
      while((c = getch()) != EOF) {
        if (c == ((keyboard_input_t*)keyboard_input)->exit_key) {
          lf_request_stop();
          break;
        }
        lf_schedule_copy(((keyboard_input_t*)keyboard_input)->keypress, 0, &c, 1);
      }
      return NULL;
    }
    // Function to direct printed messages to the curses-managed terminal.
    void print_to_terminal(const char* format, va_list args) {
      static int line_count = 1;
      move(line_count++, 0);
      vwprintw(stdscr, format, args);
      refresh();
      if(line_count >= getmaxy(stdscr)) line_count = 1;
    }
    // Function for orderly shutdown upon control-c.
    void sig_handler(int sig) {
      lf_request_stop();
    }
  =}

  reaction(startup) -> keypress {=
    initscr();  // Initialize the curses library
    cbreak();   // Disable line buffering
    noecho();   // Disable automatic echoing of typed characters
    keypad(stdscr, TRUE);  // Enable special keys

    move(0, 0);
    if(self->exit_key != 0) {
      printw("Type %c to exit.\n", self->exit_key);
    }
    refresh();

    // Register a print function handler so lf_print works.
    lf_register_print_function(print_to_terminal, LOG_LEVEL_ALL);

    if (signal(SIGINT, sig_handler) == SIG_ERR) {
      lf_print_warning("Failed to register signal handler. After exit, may have to reset terminal using 'reset'.");
    }

    static keyboard_input_t keyboard_input;
    keyboard_input.keypress = keypress;
    keyboard_input.exit_key = self->exit_key;

    // Start the thread that listens for key presses.
    lf_thread_t thread_id;
    lf_thread_create(&thread_id, &read_input, &keyboard_input);
  =}

  reaction(keypress) -> key {=
    lf_set(key, keypress->value);
  =}

  reaction(shutdown) {=
    endwin();
    lf_register_print_function(NULL, -1);
  =}
}

main reactor {
  k = new KeyboardInput(exit_key=120)

  reaction(k.key) {=
    lf_print("You typed %c (keycode %d).", k.key->value, k.key->value);
  =}
}
