target C {
  timeout: 1 s
}

reactor Source(period: time = 100 ms) {
  timer t(0, period)
  output out: int
  state count: int = 0

  reaction(t) -> out {=
    lf_set(out, self->count++);
  =}
}

reactor Sink {
  preamble {=
    #include "platform.h" // Defines PRINTF_TIME
  =}
  input in1: int
  input in2: int

  reaction(in1, in2) {=
    lf_print("--------- logical time " PRINTF_TIME, lf_time_logical_elapsed());
    if (in1->is_present) {
        lf_print("  received in1: %d", in1->value);
    }
    if (in2->is_present) {
        lf_print("  received in2: %d", in2->value);
    }
  =}
}

main reactor {
  s1 = new Source(period = 100 ms)
  s2 = new Source(period = 200 ms)
  s = new Sink()
  s1.out -> s.in1
  s2.out -> s.in2
}
